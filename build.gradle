plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'org.quiltmc'
version = '1.2.3'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    maven {
        name 'Quilt Releases'
        url 'https://maven.quiltmc.org/repository/release'
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly libs.annotations
    implementation libs.bundles.asm
    testImplementation libs.quiltflower
}

application {
    mainClass = 'org.quiltmc.draftsman.Draftsman'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.quiltmc.draftsman.Draftsman'
    }
}

java {
    withSourcesJar()
}

publishing {
    def ENV = System.getenv()
    publications {
        maven(MavenPublication) {
            groupId = 'org.quiltmc'
            artifactId = 'javadoc-draftsman'
            version = version
            from components.java
        }
    }

    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
        mavenLocal()
    }
}
